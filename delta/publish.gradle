apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/byteam/delta'
def issuesUrl = 'https://github.com/byteam/delta/issues'
def gitUrl = 'https://github.com/byteam/delta.git'

Properties config = new Properties()
config.load(project.file('../config.properties').newDataInputStream())

def GROUP = config.getProperty("GROUP")
def ARTIFACT_ID = config.getProperty("DELTA_ARTIFACT_ID")
def VERSION = config.getProperty("DELTA_VERSION")

group = GROUP
version = VERSION

install {
    repositories.mavenInstaller.pom.project {
        packaging 'aar'
        name ARTIFACT_ID
        groupId GROUP
        artifactId ARTIFACT_ID
        description 'A hot fix and incremental upgrade library for Android platform.'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'chenenyu'
                name 'Half Stack'
                email 'echocey@gmail.com'
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties bintray = new Properties()
    bintray.load(project.file('../local.properties').newDataInputStream())
    user = bintray.getProperty("bintray.user")
    key = bintray.getProperty("bintray.apikey")

    configurations = ['archives']
    publish = true
    pkg {
        repo = "maven"
        name = ARTIFACT_ID
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        userOrg = 'byteam'
        desc = "Delta."
        labels = ["gradle-plugin", "delta", "patch", "hot fix"]
        publicDownloadNumbers = true
    }
}
